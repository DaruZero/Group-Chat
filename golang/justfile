DOCKER_IMAGE := "daruzero/group-chat-go-"
DOCKER_TAG := "latest"

# Print help
@help:
	just --list

# Tidy and vendor modules
@deps: tidy vendor

# Vendor all modules
@vendor:
	go mod vendor

# Tidy modules
@tidy:
	go mod tidy

# Format all files and run fieldalignment. Run again on failure.
@fmt:
	go fmt ./...
	fieldalignment -fix ./...

# Build and push the target docker image with specified the tag (defaults to latest).
@docker target tag=DOCKER_TAG: (docker-build target) (docker-push target)

# Build target docker image with specified the tag (defaults to latest).
@docker-build target tag=DOCKER_TAG:
	docker build -t {{DOCKER_IMAGE}}{{target}}:{{tag}} -f build/{{target}}.Dockerfile .

# Push target docker image with specified the tag (defaults to latest).
@docker-push target tag=DOCKER_TAG:
	docker push {{DOCKER_IMAGE}}{{target}}:{{tag}}